function(py_set_stack_size target_name)
  if(MSVC)
    target_link_options("${target_name}" PRIVATE "/STACK:$<IF:$<CONFIG:Debug>,8000000,2000000>")
  endif()
endfunction()

add_executable(python python.c)
py_set_output_name(python python)
target_link_libraries(python PRIVATE py_default_static_options pythoncore)
target_compile_definitions(python PRIVATE _CONSOLE)
py_set_stack_size(python)

if(WIN32)
  if(DEFINED CMAKE_CONFIGURATION_TYPES)
    foreach(config_type IN LISTS CMAKE_CONFIGURATION_TYPES)
      file(WRITE "${CMAKE_RUNTIME_OUTPUT_DIRECTORY}/${config_type}/pybuilddir.txt" "\r\n")
    endforeach()
  else()
    file(WRITE "${CMAKE_RUNTIME_OUTPUT_DIRECTORY}/pybuilddir.txt" "\r\n")
  endif()
endif()

add_executable(py_testembed _testembed.c)
py_set_output_name(py_testembed _testembed)
target_link_libraries(py_testembed PRIVATE py_default_static_options pythoncore)
target_compile_definitions(py_testembed PRIVATE _CONSOLE _UNICODE UNICODE)
